#version 120

float DecodeHeightmap(vec4 heightmap)
{
	vec4 table = vec4(1.0, -1.0, 0.0, 0.0);
	return dot(heightmap, table);
}

float DecodeHeightmap(sampler2D HeightmapSampler, vec2 texcoord)
{
	vec4 heightmap = texture2D(HeightmapSampler, texcoord);
	return DecodeHeightmap(heightmap);
}

vec4 EncodeHeightmap(float fHeight)
{
	float h = fHeight;
	float positive = fHeight > 0.0 ? fHeight : 0.0;
	float negative = fHeight < 0.0 ? -fHeight : 0.0;

	vec4 color = vec4(0,0,0,0);

	color.r = positive;
	color.g = negative;
		
	return color;
}

uniform sampler2D HeightCurrentSampler;

uniform vec4  vTextureSize;
uniform float fNormalScale;

void main()
{
	vec2 offset[4] = vec2[4] (
		vec2(-1.0, 0.0),
		vec2( 1.0, 0.0),
		vec2( 0.0,-1.0),
		vec2( 0.0, 1.0)
	);

	float fHeightL = DecodeHeightmap(HeightCurrentSampler, gl_TexCoord[0].xy + offset[0]*vTextureSize.xy);
	float fHeightR = DecodeHeightmap(HeightCurrentSampler, gl_TexCoord[0].xy + offset[1]*vTextureSize.xy);
	float fHeightT = DecodeHeightmap(HeightCurrentSampler, gl_TexCoord[0].xy + offset[2]*vTextureSize.xy);
	float fHeightB = DecodeHeightmap(HeightCurrentSampler, gl_TexCoord[0].xy + offset[3]*vTextureSize.xy);
	
	vec3 n = vec3(fHeightB - fHeightT, fHeightR - fHeightL, fNormalScale);
	vec3 normal = (n + 1.0) * 0.5;
	
	gl_FragColor = vec4(normal.rgb, 1.0);
}
